plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

group = 'gg.launchblock'
version = 'v0.1.0'

archivesBaseName = 'sdk-java'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()  // Use Maven Central repository
}

dependencies {
    implementation 'org.apache.kafka:kafka-clients:3.9.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'org.slf4j:slf4j-simple:2.0.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'gg.launchblock'
            artifactId = 'sdk-java'

            from components.java
            
            pom {
                name = 'LaunchBlock Java SDK'
                description = 'A Java SDK for interacting with our API'
                url = 'https://github.com/LaunchBlockMC/sdk-java'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/aalmiray/app.git'
                    developerConnection = 'scm:git:ssh://github.com/aalmiray/app.git'
                    url = 'http://github.com/aalmiray/app'
                }
                developers {
                    developer {
                        id = 'LaunchBlockMC'
                        name = 'LaunchBlock Team'
                        email = 'help@launchblock.gg'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}